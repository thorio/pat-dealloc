# Maintainer: thorou <thorou@chirality.de>

_pkgname=pat-dealloc
pkgname=$_pkgname-dkms
pkgver=0.1.0
pkgrel=1
pkgdesc="Kernel module to allow removal of x86 PAT entries from userspace"
url="https://github.com/thorio/$_pkgname"
license=("GPL3")
arch=("x86_64")
depends=("dkms")
makedepends=("cargo")
source=("$pkgname::git+https://github.com/thorio/pat-dealloc.git")
sha256sums=("SKIP")

build() {
	cd $pkgname

	export RUSTUP_TOOLCHAIN=stable
	cargo build --frozen --release --package pat-dealloc-bin
}

package() {
	install -Dm755 "$pkgname/target/release/pat-dealloc" "$pkgdir/usr/bin/pat-dealloc"
	dkms_dest="${pkgdir}/usr/src/${_pkgname}-${pkgver}"

	cat > dkms.conf <<EOF
PACKAGE_NAME="$_pkgname"
PACKAGE_VERSION="$pkgver"
MAKE[0]="make"
CLEAN="make clean"
BUILT_MODULE_NAME[0]="${_pkgname/-/_}"
DEST_MODULE_LOCATION[0]="/kernel/drivers/misc"
AUTOINSTALL="yes"
EOF

	install -Dm664 dkms.conf "$dkms_dest/dkms.conf"
	install -Dm664 "$pkgname/module/Makefile" "$dkms_dest/Makefile"
	install -Dm664 "$pkgname/module/pat_dealloc.c" "$dkms_dest/pat_dealloc.c"
}
